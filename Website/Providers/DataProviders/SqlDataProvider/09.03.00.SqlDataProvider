/************************************************************/
/*****              SqlDataProvider                     *****/
/*****                                                  *****/
/*****                                                  *****/
/***** Note: To manually execute this script you must   *****/
/*****       perform a search and replace operation     *****/
/*****       for {databaseOwner} and {objectQualifier}  *****/
/*****                                                  *****/
/************************************************************/

IF EXISTS (SELECT * FROM dbo.sysobjects
	WHERE id = object_id(N'{databaseOwner}[{objectQualifier}HostSettings]') and OBJECTPROPERTY(id, N'IsUserTable') = 1)
BEGIN
	IF NOT EXISTS (SELECT * FROM {databaseOwner}[{objectQualifier}HostSettings] WHERE [SettingName] = N'PurgeEventLogBatchCount')
		INSERT INTO {databaseOwner}[{objectQualifier}HostSettings] 
			([SettingName]
			,[SettingValue]
			,[SettingIsSecure]
			,[CreatedByUserID]
			,[CreatedOnDate]
			,[LastModifiedByUserID]
			,[LastModifiedOnDate])
		VALUES (N'PurgeEventLogBatchCount', N'150000', 0, -1, GETDATE(), NULL, NULL)
END

GO

IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}[{objectQualifier}PurgeEventLog]') AND OBJECTPROPERTY(id, N'IsPROCEDURE') = 1)
  DROP PROCEDURE {databaseOwner}[{objectQualifier}PurgeEventLog]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}PurgeEventLog]
AS
	DECLARE @PurgeBatchCount INT = ISNULL(
	(SELECT CAST(SettingValue AS INT) 
		FROM {databaseOwner}[{objectQualifier}HostSettings]
		WHERE SettingName = N'PurgeEventLogBatchCount' AND ISNUMERIC(SettingValue) > 0), 150000)

	;WITH logcounts AS
	(  
	  SELECT TOP(@PurgeBatchCount)
		LogConfigID, 
		ROW_NUMBER() OVER(PARTITION BY LogConfigID ORDER BY LogCreateDate DESC) AS logEventSequence	
	  FROM {databaseOwner}[{objectQualifier}EventLog]
	)
	DELETE lc
	FROM logcounts lc
		INNER JOIN {databaseOwner}[{objectQualifier}EventLogConfig] elc ON elc.ID = lc.LogConfigID
	WHERE elc.KeepMostRecent <> -1
		AND lc.logEventSequence > elc.KeepMostRecent

GO

/* Due to DNN-21554 we could have orphaned users in apsnet tables, below SP removes such users if there are some */
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}[{objectQualifier}DeleteOrphanedAspNetUsers]') AND OBJECTPROPERTY(id, N'IsPROCEDURE') = 1)
  DROP PROCEDURE {databaseOwner}[{objectQualifier}DeleteOrphanedAspNetUsers]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}DeleteOrphanedAspNetUsers]
    @ApplicationName NVARCHAR(256)
AS

IF (SELECT COUNT(*) FROM dbo.sysobjects
	WHERE id = object_id(N'dbo.[aspnet_Users]') OR
		  id = object_id(N'dbo.[aspnet_Membership]') OR
		  id = object_id(N'{databaseOwner}[{objectQualifier}Users]') and OBJECTPROPERTY(id, N'IsUserTable') = 1) = 3
BEGIN
	BEGIN TRANSACTION
		BEGIN TRY
			-- remove orphaned aspnet memberships
			;WITH
			orphaned_aspnet_memberships(UserId)
			AS
			(SELECT asp_memberships.UserId FROM dbo.aspnet_Membership asp_memberships
					INNER JOIN dbo.aspnet_Applications asp_apps ON asp_apps.ApplicationId = asp_memberships.ApplicationId
					INNER JOIN dbo.aspnet_Users asp_users ON asp_memberships.UserId = asp_users.UserId
					LEFT JOIN {databaseOwner}{objectQualifier}Users users ON asp_users.UserName = users.Username
					WHERE asp_apps.LoweredApplicationName = LOWER(@ApplicationName) AND users.Username IS NULL)
			DELETE m FROM dbo.aspnet_Membership m
			INNER JOIN orphaned_aspnet_memberships o ON m.UserId = o.UserId

			-- remove orphaned aspnet users
			;WITH
			orphaned_aspnet_users (UserId)
			AS 
			(SELECT asp_users.UserId FROM dbo.aspnet_Users asp_users
					INNER JOIN dbo.aspnet_Applications asp_apps ON asp_apps.ApplicationId = asp_users.ApplicationId
					LEFT JOIN {databaseOwner}{objectQualifier}Users users ON asp_users.UserName = users.Username		
					WHERE asp_apps.LoweredApplicationName = LOWER(@ApplicationName) AND users.Username IS NULL)
			DELETE u FROM dbo.aspnet_Users u
			INNER JOIN orphaned_aspnet_users o ON u.UserId = o.UserId
		END TRY
		BEGIN CATCH
			SELECT ERROR_MESSAGE() AS ErrorMessage
			IF @@TRANCOUNT > 0  
				ROLLBACK TRANSACTION
		END CATCH
	IF @@TRANCOUNT > 0 
	    COMMIT TRANSACTION
END

GO

/***** DNN-24542: Journal_GET SP Performance: Timing out when 1k+ Friends. *****/
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}Journal_Get]') AND type in (N'P', N'PC'))
	DROP PROCEDURE {databaseOwner}[{objectQualifier}Journal_Get]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}Journal_Get]
    @PortalId INT,
    @CurrentUserId INT,
    @JournalId INT,
    @IncludeAllItems INT = 0,
    @IsDeleted INT = 0,
    @SecurityCheck BIT = 0
    AS
    DECLARE @RegisteredRoleId INT
    SELECT  @RegisteredRoleId = RegisteredRoleId FROM {databaseOwner}[{objectQualifier}Portals] WHERE PortalID = @PortalId
    SELECT  	j.JournalId, 
				j.JournalTypeId, 
				j.Title, 
				j.Summary, 
				j.UserId, 
				j.DateCreated, 
				j.DateUpdated, 
				j.PortalId,
				j.ProfileId, 
				j.GroupId, 
				j.ObjectKey, 
				j.AccessKey,
				'JournalOwner' = '<entity><id>' + CAST(p.UserId as nvarchar(150)) + '</id><name><![CDATA[' + p.DisplayName + ']]></name></entity>',
				'JournalAuthor' = CASE WHEN ISNULL(a.UserId, -1) > 0 THEN '<entity><id>' + CAST(a.UserId as nvarchar(150)) + '</id><name><![CDATA[' + a.DisplayName + ']]></name></entity>' ELSE '' END,
				'JournalOwnerId' = ISNULL(j.ProfileId,j.UserId),
				jt.Icon, 
				jt.JournalType,
				'Profile' = CASE WHEN j.ProfileId > 0 THEN '<entity><id>' + CAST(p.UserID as nvarchar(150)) + '</id><name><![CDATA[' + p.DisplayName + ']]></name><vanity></vanity></entity>' ELSE '' END,
				'SimilarCount' = (SELECT COUNT(JournalId) FROM {databaseOwner}{objectQualifier}Journal WHERE ContentItemId = j.ContentItemId AND JournalTypeId = j.JournalTypeId),
				jd.JournalXML, 
				ContentItemId, 
				j.ItemData, 
				j.IsDeleted, 
				j.CommentsDisabled, 
				j.CommentsHidden
    FROM  	    {databaseOwner}[{objectQualifier}Journal] AS j
				INNER JOIN {databaseOwner}[{objectQualifier}Journal_Types] as jt ON jt.JournalTypeId = j.JournalTypeId
				LEFT JOIN {databaseOwner}[{objectQualifier}Journal_Security] AS js ON js.JournalId = j.JournalId
				LEFT JOIN {databaseOwner}[{objectQualifier}Journal_User_Permissions](@PortalId,@CurrentUserId, @RegisteredRoleId) as t ON t.seckey = js.SecurityKey
				LEFT JOIN {databaseOwner}[{objectQualifier}Journal_Data] as jd on jd.JournalId = j.JournalId 
				LEFT JOIN {databaseOwner}[{objectQualifier}Users] AS p ON j.ProfileId = p.UserID 
				LEFT JOIN {databaseOwner}[{objectQualifier}Users] AS a ON j.UserId = a.UserID
    WHERE		(@SecurityCheck = 0 OR (@SecurityCheck = 1 AND t.seckey IS NOT NULL)) AND
				((@IncludeAllItems = 0) AND (j.JournalId = @JournalId AND j.IsDeleted = @IsDeleted)) OR 
				((@IncludeAllItems = 1) AND (j.JournalId = @JournalId))
GO

/************************************************************/
/*****              SqlDataProvider                     *****/
/************************************************************/
/************************************************************/

