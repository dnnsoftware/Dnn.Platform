/************************************************************/
/*****              SqlDataProvider                     *****/
/*****                                                  *****/
/*****                                                  *****/
/***** Note: To manually execute this script you must   *****/
/*****       perform a search and replace operation     *****/
/*****       for {databaseOwner} and {objectQualifier}  *****/
/*****                                                  *****/
/************************************************************/

IF EXISTS (SELECT * FROM dbo.sysobjects
	WHERE id = object_id(N'{databaseOwner}[{objectQualifier}HostSettings]') and OBJECTPROPERTY(id, N'IsUserTable') = 1)
BEGIN
	IF NOT EXISTS (SELECT * FROM {databaseOwner}[{objectQualifier}HostSettings] WHERE [SettingName] = N'PurgeEventLogBatchCount')
		INSERT INTO {databaseOwner}[{objectQualifier}HostSettings] 
			([SettingName]
			,[SettingValue]
			,[SettingIsSecure]
			,[CreatedByUserID]
			,[CreatedOnDate]
			,[LastModifiedByUserID]
			,[LastModifiedOnDate])
		VALUES (N'PurgeEventLogBatchCount', N'150000', 0, -1, GETDATE(), NULL, NULL)
END

GO

IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}[{objectQualifier}PurgeEventLog]') AND OBJECTPROPERTY(id, N'IsPROCEDURE') = 1)
  DROP PROCEDURE {databaseOwner}[{objectQualifier}PurgeEventLog]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}PurgeEventLog]
AS
	DECLARE @PurgeBatchCount INT = ISNULL(
	(SELECT CAST(SettingValue AS INT) 
		FROM {databaseOwner}[{objectQualifier}HostSettings]
		WHERE SettingName = N'PurgeEventLogBatchCount' AND ISNUMERIC(SettingValue) > 0), 150000)

	;WITH logcounts AS
	(  
	  SELECT TOP(@PurgeBatchCount)
		LogConfigID, 
		ROW_NUMBER() OVER(PARTITION BY LogConfigID ORDER BY LogCreateDate DESC) AS logEventSequence	
	  FROM {databaseOwner}[{objectQualifier}EventLog]
	)
	DELETE lc
	FROM logcounts lc
		INNER JOIN {databaseOwner}[{objectQualifier}EventLogConfig] elc ON elc.ID = lc.LogConfigID
	WHERE elc.KeepMostRecent <> -1
		AND lc.logEventSequence > elc.KeepMostRecent

GO

/* Due to DNN-21554 we could have orphaned users in apsnet tables, below SP removes such users if there are some */
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}[{objectQualifier}DeleteOrphanedAspNetUsers]') AND OBJECTPROPERTY(id, N'IsPROCEDURE') = 1)
  DROP PROCEDURE {databaseOwner}[{objectQualifier}DeleteOrphanedAspNetUsers]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}DeleteOrphanedAspNetUsers]
    @ApplicationName NVARCHAR(256)
AS

IF (SELECT COUNT(*) FROM dbo.sysobjects
	WHERE id = object_id(N'dbo.[aspnet_Users]') OR
		  id = object_id(N'dbo.[aspnet_Membership]') OR
		  id = object_id(N'{databaseOwner}[{objectQualifier}Users]') and OBJECTPROPERTY(id, N'IsUserTable') = 1) = 3
BEGIN
	BEGIN TRANSACTION
		BEGIN TRY
			-- remove orphaned aspnet memberships
			;WITH
			orphaned_aspnet_memberships(UserId)
			AS
			(SELECT asp_memberships.UserId FROM dbo.aspnet_Membership asp_memberships
					INNER JOIN dbo.aspnet_Applications asp_apps ON asp_apps.ApplicationId = asp_memberships.ApplicationId
					INNER JOIN dbo.aspnet_Users asp_users ON asp_memberships.UserId = asp_users.UserId
					LEFT JOIN {databaseOwner}{objectQualifier}Users users ON asp_users.UserName = users.Username
					WHERE asp_apps.LoweredApplicationName = LOWER(@ApplicationName) AND users.Username IS NULL)
			DELETE m FROM dbo.aspnet_Membership m
			INNER JOIN orphaned_aspnet_memberships o ON m.UserId = o.UserId

			-- remove orphaned aspnet users
			;WITH
			orphaned_aspnet_users (UserId)
			AS 
			(SELECT asp_users.UserId FROM dbo.aspnet_Users asp_users
					INNER JOIN dbo.aspnet_Applications asp_apps ON asp_apps.ApplicationId = asp_users.ApplicationId
					LEFT JOIN {databaseOwner}{objectQualifier}Users users ON asp_users.UserName = users.Username		
					WHERE asp_apps.LoweredApplicationName = LOWER(@ApplicationName) AND users.Username IS NULL)
			DELETE u FROM dbo.aspnet_Users u
			INNER JOIN orphaned_aspnet_users o ON u.UserId = o.UserId
		END TRY
		BEGIN CATCH
			SELECT ERROR_MESSAGE() AS ErrorMessage
			IF @@TRANCOUNT > 0  
				ROLLBACK TRANSACTION
		END CATCH
	IF @@TRANCOUNT > 0 
	    COMMIT TRANSACTION
END

GO

/***** DNN-22117: update sp GetFiles to get files from folder and all sub folders in one time. *****/
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}GetFiles]') AND type in (N'P', N'PC'))
    DROP PROCEDURE {databaseOwner}[{objectQualifier}GetFiles]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}GetFiles]
    @FolderId INT,
    @RetrieveUnpublishedFiles BIT = 0,
    @Recursive BIT = 0
AS
    DECLARE @FolderPath NVARCHAR(300)
    DECLARE @PortalId INT
    SELECT @FolderPath = FolderPath, @PortalId = ISNULL(PortalId, -1) FROM {databaseOwner}[{objectQualifier}Folders] WHERE FolderId = @FolderId
    IF @RetrieveUnpublishedFiles = 0 
    BEGIN
        SELECT
            F.FileId,
            F.PortalId,
            F.[FileName],
            F.Extension,
            F.[Size],
            F.Width,
            F.Height,
            F.ContentType,
            F.FolderID,
            F.Folder,
            F.StorageLocation,
            F.IsCached,
            F.FolderMappingID,
            F.UniqueId,
            F.VersionGuid,
            F.SHA1Hash,
            F.LastModificationTime,
            F.Title,
            F.Description,
            F.EnablePublishPeriod,
            F.StartDate,
            F.EndDate,
            F.CreatedByUserID,
            F.CreatedOnDate,
            F.LastModifiedByUserID,
            F.LastModifiedOnDate,
            F.PublishedVersion,
            F.ContentItemID,
            F.HasBeenPublished
        FROM {databaseOwner}[{objectQualifier}vw_PublishedFiles] F            
        WHERE ((@Recursive = 1 AND ISNULL(F.PortalId, -1) = @PortalId AND F.Folder LIKE @FolderPath + '%') OR F.FolderID = @FolderID)
        ORDER BY [FolderID], [FileName]
    END
    ELSE BEGIN
        SELECT
            F.FileId,
            F.PortalId,
            F.[FileName],
            F.Extension,
            F.[Size],
            F.Width,
            F.Height,
            F.ContentType,
            F.FolderID,
            F.Folder,
            F.StorageLocation,
            F.IsCached,
            F.FolderMappingID,
            F.UniqueId,
            F.VersionGuid,
            F.SHA1Hash,
            F.LastModificationTime,
            F.Title,
            F.Description,
            F.EnablePublishPeriod,
            F.StartDate,
            F.EndDate,
            F.CreatedByUserID,
            F.CreatedOnDate,
            F.LastModifiedByUserID,
            F.LastModifiedOnDate,
            F.PublishedVersion,
            F.ContentItemID,
            F.HasBeenPublished
        FROM {databaseOwner}[{objectQualifier}vw_Files] F            
        WHERE ((@Recursive = 1 AND ISNULL(F.PortalId, -1) = @PortalId AND F.Folder LIKE @FolderPath + '%') OR F.FolderID = @FolderID)
        ORDER BY [FolderID], [FileName]
    END
GO

/************************************************************/
/*****              SqlDataProvider                     *****/
/************************************************************/
/************************************************************/

