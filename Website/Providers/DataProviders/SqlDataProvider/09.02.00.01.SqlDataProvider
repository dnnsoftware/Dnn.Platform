/************************************************************/
/*****              SqlDataProvider                     *****/
/*****                                                  *****/
/*****                                                  *****/
/***** Note: To manually execute this script you must   *****/
/*****       perform a search and replace operation     *****/
/*****       for {databaseOwner} and {objectQualifier}  *****/
/*****                                                  *****/
/************************************************************/

/* DNN-10072 Incorrect assembly version check */
/**********************************************/

IF (OBJECT_ID(N'{databaseOwner}[{objectQualifier}fn_ParseVersion]') IS NOT NULL)
  DROP FUNCTION {databaseOwner}[{objectQualifier}fn_ParseVersion];
GO

CREATE FUNCTION {databaseOwner}[{objectQualifier}fn_ParseVersion]
(
	@Version	nvarchar(20)
)
RETURNS @VersionParts TABLE (Major int, Minor int, Build int)
WITH SCHEMABINDING
AS
	BEGIN
		DECLARE @Pos int;
		DECLARE @String nvarchar(20);
		DECLARE @Major int;
		DECLARE @Minor int;
		DECLARE @Build int;

		SET @String = @Version;
		SET @Pos = CHARINDEX('.' , @String);
		SET @Major = CONVERT(int, LEFT(@String, @Pos - 1));
		SET @String = STUFF(@String, 1, @Pos, '');
		SET @Pos = CHARINDEX('.' , @String);
		SET @Minor = CONVERT(int, LEFT(@String, @Pos - 1));
		SET @String = STUFF(@String, 1, @Pos, '');
		SET @Build = CONVERT(int, @String);

		INSERT INTO @VersionParts(Major, Minor, Build)
		VALUES (@Major, @Minor, @Build);

		RETURN;
	END
GO

IF (OBJECT_ID(N'{databaseOwner}[{objectQualifier}fn_CompareVersion]') IS NOT NULL)
  DROP FUNCTION {databaseOwner}[{objectQualifier}fn_CompareVersion];
GO

CREATE FUNCTION {databaseOwner}[{objectQualifier}fn_CompareVersion]
(
	@Version		nvarchar(20),
	@CurrentVersion nvarchar(20)
)
RETURNS int

AS
	BEGIN

		DECLARE @MajorVersion int
		DECLARE @MajorCurrentVersion int
		DECLARE @MinorVersion int
		DECLARE @MinorCurrentVersion int
		DECLARE @BuildVersion int
		DECLARE @BuildCurrentVersion int

		SELECT @MajorVersion = Major, @MinorVersion = Minor, @BuildVersion = Build
		FROM {databaseOwner}[{objectQualifier}fn_ParseVersion](@Version)
		SELECT @MajorCurrentVersion = Major, @MinorCurrentVersion = Minor, @BuildCurrentVersion = Build
		FROM {databaseOwner}[{objectQualifier}fn_ParseVersion](@CurrentVersion)

		IF @CurrentVersion IS NULL
			-- Assembly Not Registered -  Set ReturnCode = 0, so assembly is copied
			RETURN 0
		ELSE
			IF @Version = @CurrentVersion
				-- Same Version - Set ReturnCode = 2, so assembly is only copied on repair
				RETURN 2
			ELSE
				-- Different Version
				-- Compare Major, Minor, Revision
				IF @MajorVersion > @MajorCurrentVersion
					OR (@MajorVersion = @MajorCurrentVersion AND @MinorVersion > @MinorCurrentVersion)
						OR (@MajorVersion = @MajorCurrentVersion AND @MinorVersion = @MinorCurrentVersion AND @BuildVersion > @BuildCurrentVersion)
					-- Newer version - at least on of Major, Minor, Revision is larger - Set ReturnCode = 1, so assembly is copied
					RETURN 1
				ELSE
					-- Older Version - Set ReturnCode = 3, so assembly is not copied
					RETURN 3

		RETURN 3
	END
GO

IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}[{objectQualifier}RegisterAssembly]') AND OBJECTPROPERTY(id, N'IsPROCEDURE') = 1)
  DROP PROCEDURE {databaseOwner}[{objectQualifier}RegisterAssembly]
GO

CREATE procedure {databaseOwner}[{objectQualifier}RegisterAssembly]
	@PackageID      int,
	@AssemblyName   nvarchar(250),
	@Version		nvarchar(20)
As
	DECLARE @AssemblyID int
	DECLARE @CurrentVersion nvarchar(20)
	/*	@ReturnCode Values
		0 - Assembly Does not Exist
		1 - Older Version of Assembly Exists
		2 - Assembly Already Registered - Version = CurrentVersion
		3 - Assembly Already Registered - Version < CurrentVersion
	*/
	DECLARE @CompareVersion int

	-- First check if this assembly is registered to this package
	SET @AssemblyID = (SELECT AssemblyID
							FROM {databaseOwner}{objectQualifier}Assemblies
							WHERE PackageID = @PackageID
								AND AssemblyName = @AssemblyName)

	IF @AssemblyID IS NULL
		BEGIN
			-- AssemblyID is null (not registered)
			-- but assembly may be registerd by other packages so check for Max unstalled version
			SET @CurrentVersion  = (SELECT TOP 1 a.Version
									FROM {databaseOwner}{objectQualifier}Assemblies a
									CROSS APPLY {databaseOwner}[{objectQualifier}fn_ParseVersion](a.Version) AS v
									WHERE a.AssemblyName = @AssemblyName
									ORDER BY v.Major DESC, v.Minor DESC, v.Build DESC)

			SET @CompareVersion = {databaseOwner}{objectQualifier}fn_CompareVersion(@Version, @CurrentVersion)

			-- Add an assembly regsitration for this package
			INSERT INTO {databaseOwner}{objectQualifier}Assemblies (
				PackageID,
				AssemblyName,
				Version
			)
			VALUES (
				@PackageID,
				@AssemblyName,
				@Version
			)
		END
	ELSE
		BEGIN
			-- AssemblyID is not null - Assembly is registered - test for version
			SET @CurrentVersion  = (SELECT TOP 1 a.Version
									FROM {databaseOwner}{objectQualifier}Assemblies a
									CROSS APPLY {databaseOwner}[{objectQualifier}fn_ParseVersion](a.Version) AS v
									WHERE a.AssemblyID = @AssemblyID
									ORDER BY v.Major DESC, v.Minor DESC, v.Build DESC)

			SET @CompareVersion = {databaseOwner}{objectQualifier}fn_CompareVersion(@Version, @CurrentVersion)

			IF @CompareVersion = 1
				BEGIN
					-- Newer version - Update Assembly registration
					UPDATE {databaseOwner}{objectQualifier}Assemblies
					SET    Version = @Version
					WHERE  AssemblyID = @AssemblyID
				END
		END

	SELECT @CompareVersion
GO
/************************************************************/
/*****              SqlDataProvider                     *****/
/************************************************************/
