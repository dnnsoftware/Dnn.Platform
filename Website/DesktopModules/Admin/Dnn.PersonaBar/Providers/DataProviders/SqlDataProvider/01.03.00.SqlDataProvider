/************************************************************/
/*****              SqlDataProvider                     *****/
/*****                                                  *****/
/*****                                                  *****/
/***** Note: To manually execute this script you must   *****/
/*****       perform a search and replace operation     *****/
/*****       for {databaseOwner} and {objectQualifier}  *****/
/*****                                                  *****/
/************************************************************/

/***** PAE-217: add icon file column. *****/
IF NOT EXISTS( SELECT * FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = '{objectQualifier}PersonaBarMenu' AND  COLUMN_NAME = 'IconFile')
   BEGIN
        ALTER TABLE {databaseOwner}{objectQualifier}PersonaBarMenu ADD IconFile NVARCHAR(260) NULL
    END 
GO

IF EXISTS (SELECT * FROM dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}PersonaBar_SavePersonaBarMenu') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
    DROP PROCEDURE {databaseOwner}{objectQualifier}PersonaBar_SavePersonaBarMenu
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}PersonaBar_SavePersonaBarMenu]
    @Identifier                 NVARCHAR(50),
    @ModuleName                 NVARCHAR(50),
    @FolderName               NVARCHAR(50),
    @Controller                 NVARCHAR(260),
    @ResourceKey                NVARCHAR(50),
    @Path                       NVARCHAR(260),
    @Link                       NVARCHAR(260),
    @CssClass                   NVARCHAR(50),
    @ParentId                   INT,
    @Order                      INT,
    @AllowHost                  BIT,
    @Enabled                    BIT,
    @CurrentUserId              INT,
    @IconFile                   NVARCHAR(260) = ''
AS
BEGIN
    IF EXISTS(SELECT Identifier FROM {databaseOwner}[{objectQualifier}PersonaBarMenu] WHERE Identifier = @Identifier)
    BEGIN
        UPDATE {databaseOwner}[{objectQualifier}PersonaBarMenu]
            SET 
                ModuleName = @ModuleName,
                FolderName = @FolderName,
                ResourceKey = @ResourceKey,
                Path = @Path,
                Link = @Link,
                CssClass = @CssClass,
                IconFile = @IconFile,
                ParentId = @ParentId,
                [Order] = @Order,
                AllowHost = @AllowHost,
                Enabled = @Enabled,
                LastModifiedByUserId = CASE WHEN @CurrentUserId = -1 THEN NULL ELSE @CurrentUserId END,
                LastModifiedOnDate = GETDATE()
            WHERE Identifier = @Identifier

            SELECT MenuId FROM {databaseOwner}[{objectQualifier}PersonaBarMenu] WHERE Identifier = @Identifier
    END
    ELSE
    BEGIN
        INSERT INTO {databaseOwner}[{objectQualifier}PersonaBarMenu] (
            [Identifier],
            [ModuleName],
            [FolderName],
            [Controller],
            [ResourceKey],
            [Path],
            [Link],
            [CssClass],
            [IconFile],
            [ParentId],
            [Order],
            [AllowHost],
            [Enabled],
            [CreatedByUserId],
            [CreatedOnDate],
            [LastModifiedByUserId],
            [LastModifiedOnDate]
        ) VALUES (
            @Identifier,
            @ModuleName,
            @FolderName,
            @Controller,
            @ResourceKey,
            @Path,
            @Link,
            @CssClass,
            @IconFile,
            @ParentId,
            @Order,
            @AllowHost,
            @Enabled,
            CASE WHEN @CurrentUserId = -1 THEN NULL ELSE @CurrentUserId END,
            GETDATE(),
            CASE WHEN @CurrentUserId = -1 THEN NULL ELSE @CurrentUserId END,
            GETDATE()
        )

        SELECT SCOPE_IDENTITY()
    END
END
GO

IF EXISTS (SELECT * FROM dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}PersonaBar_GetPersonaBarMenu') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
    DROP PROCEDURE {databaseOwner}{objectQualifier}PersonaBar_GetPersonaBarMenu
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}PersonaBar_GetPersonaBarMenu]
AS
	SELECT 
        MenuId, 
        Identifier, 
        ModuleName, 
        FolderName,
        Controller, 
        ResourceKey, 
        Path, 
        Link, 
        CssClass, 
        IconFile,
        ParentId, 
        [Order], 
        AllowHost, 
        Enabled, 
        CreatedByUserId, 
        CreatedOnDate, 
        LastModifiedByUserId, 
        LastModifiedOnDate 
        
    FROM {databaseOwner}[{objectQualifier}PersonaBarMenu]
GO

UPDATE {databaseOwner}[{objectQualifier}PersonaBarMenu] SET IconFile = N'Images/nav_' + Identifier + '.svg'
    WHERE Identifier IN ('Content','Manage','Settings','Edit')
GO

/***** DNN-9665: remove logout menu item as it will used in flyout menu instead. *****/
EXEC {databaseOwner}[{objectQualifier}PersonaBar_DeletePersonaBarMenuByIdentifier] @Identifier = N'Logout'
GO

/************************************************************/
/*****              SqlDataProvider                     *****/
/************************************************************/