/************************************************************/
/*****              SqlDataProvider                     *****/
/*****                                                  *****/
/*****                                                  *****/
/***** Note: To manually execute this script you must   *****/
/*****       perform a search and replace operation     *****/
/*****       for {databaseOwner} and {objectQualifier}  *****/
/*****                                                  *****/
/************************************************************/

/** DNN-10073 Update to support keyword search **/
/** DNN-10154 Added option to include super users in search result **/
IF EXISTS (SELECT * FROM dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}Personabar_GetUsers') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}Personabar_GetUsers
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Personabar_GetUsers (
	@PortalId INT,
	@SortColumn NVARCHAR(32),
	@SortAscending BIT,
    @PageIndex int = 0,
    @PageSize int = 10,
	@Keyword NVARCHAR(128),
	@IncludeUnauthorized bit,
	@IncludeDeleted bit,
	@IncludeSuperUsers bit,
	@FilterId int -- Authorized = 0, UnAuthorized = 1, Deleted = 2, SuperUsers = 3, RegisteredUsers = 4, All = 5
) AS
BEGIN
	IF @FilterId = 0 -- must exclude unauthorized users for authorized filter
	BEGIN
		SET @IncludeUnauthorized = 0
	END
	ELSE IF @FilterId = 1 -- must include unauthorized users for unauthorized filter
	BEGIN
		SET @IncludeUnauthorized = 1
	END
	ELSE IF @FilterId = 2 -- must include deleted users for deleted filter
	BEGIN
		SET @IncludeDeleted = 1
	END

	SELECT COUNT(*) AS TotalRecords
	FROM {databaseOwner}{objectQualifier}UserPortals UP WITH (NOLOCK)
	INNER JOIN {databaseOwner}{objectQualifier}vw_Users U WITH (NOLOCK) ON UP.UserId = U.UserID AND (@IncludeSuperUsers = 1 or U.PortalId = @PortalId)
	WHERE UP.PortalId = @PortalId
	AND (@IncludeSuperUsers = 1 OR (@IncludeSuperUsers = 0 and U.IsSuperUser = 0))
	AND (
			(
				IsNull(@Keyword, N'') != N'' 
				AND (u.DisplayName LIKE @Keyword OR u.FirstName LIKE @Keyword OR u.LastName LIKE @Keyword OR u.Username LIKE @Keyword OR u.Email LIKE @Keyword)
			) 
			OR IsNull(@Keyword, N'') = N''
		)
	AND (@IncludeUnauthorized = 1 OR (@IncludeUnauthorized = 0 AND (U.Authorised = 1 OR UP.Authorised = 1)))
	AND (@IncludeDeleted = 1 OR (@IncludeDeleted = 0 AND U.IsDeleted = 0))
	AND (
		(@FilterId = 0 AND (U.Authorised = 1 OR UP.Authorised = 1)) OR 
		(@FilterId = 1 AND UP.Authorised <> 1) OR 
		(@FilterId = 2 AND U.IsDeleted = 1) OR 
		(@FilterId = 3 AND U.IsSuperUser = 1) OR 
		(@FilterId = 5)
	)
		
	;WITH UsersCTE
	AS
	(	
		SELECT U.UserID, U.Username, U.DisplayName, U.Email, U.CreatedOnDate, U.IsDeleted, U.Authorised, U.IsSuperUser,
		ROW_NUMBER() OVER ( ORDER BY CASE WHEN @SortColumn = 'Joined' AND @SortAscending = 1 THEN U.UserID END ASC, 
									 CASE WHEN @SortColumn = 'Joined' AND @SortAscending = 0 THEN U.UserID END DESC,
									 CASE WHEN @SortColumn = 'Email' AND @SortAscending = 1 THEN U.Email END ASC, 
									 CASE WHEN @SortColumn = 'Email' AND @SortAscending = 0 THEN U.Email END DESC,
									 CASE WHEN @SortColumn = 'DisplayName' AND @SortAscending = 1 THEN U.DisplayName END ASC, 
									 CASE WHEN @SortColumn = 'DisplayName' AND @SortAscending = 0 THEN U.DisplayName END DESC) AS RowNumber 
		FROM {databaseOwner}{objectQualifier}vw_Users U WITH (NOLOCK)
		INNER JOIN {databaseOwner}{objectQualifier}UserPortals UP WITH (NOLOCK)
			ON U.UserID = UP.UserId
			AND (@IncludeSuperUsers = 1 or U.PortalId = @PortalId)
		WHERE UP.PortalId = @PortalId
		AND (@IncludeSuperUsers = 1 OR (@IncludeSuperUsers = 0 and U.IsSuperUser = 0))
		AND (
				(
					IsNull(@Keyword, N'') != N'' 
					AND (u.DisplayName LIKE @Keyword OR u.FirstName LIKE @Keyword OR u.LastName LIKE @Keyword OR u.Username LIKE @Keyword OR u.Email LIKE @Keyword)
				) 
				OR IsNull(@Keyword, N'') = N''
			)
		AND (@IncludeUnauthorized = 1 OR (@IncludeUnauthorized = 0 AND (U.Authorised = 1 OR UP.Authorised = 1)))
		AND (@IncludeDeleted = 1 OR (@IncludeDeleted = 0 AND U.IsDeleted = 0))
		AND (
			(@FilterId = 0 AND (U.Authorised = 1 OR UP.Authorised = 1)) OR 
			(@FilterId = 1 AND UP.Authorised <> 1) OR 
			(@FilterId = 2 AND U.IsDeleted = 1) OR 
			(@FilterId = 3 AND U.IsSuperUser = 1) OR 
			(@FilterId = 5)
		)
	)
	SELECT * FROM UsersCTE
	WHERE	RowNumber BETWEEN (@PageIndex * @PageSize + 1) AND ((@PageIndex + 1) * @PageSize);
END
GO


/************************************************************/
/*****              SqlDataProvider                     *****/
/************************************************************/